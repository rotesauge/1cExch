Функция Записать(СтруктураДанных, ПараметрыЗаписи=Неопределено, ПараметрыСериализации=Неопределено, ПроверятьСтруктуру=Ложь, ПараметрыФайла=Неопределено) Экспорт
	#Если ВебКлиент И НЕ Сервер Тогда
		Возврат JSON_Сервер.Записать(СтруктураДанных, УдалитьПробелыИПереносыСтрок);
	#КонецЕсли

	//Параметры записи
	Если НЕ ТипЗнч(ПараметрыЗаписи)=Тип("Структура") Тогда
		ПараметрыЗаписи=Новый Структура;
	КонецЕсли;
	Если НЕ ПараметрыЗаписи.Свойство("ПереносСтрок") Тогда
		ПараметрыЗаписи.Вставить("ПереносСтрок", ПереносСтрокJSON.Авто);
	КонецЕсли;
	Если НЕ ПараметрыЗаписи.Свойство("СимволыОтступа") Тогда
		ПараметрыЗаписи.Вставить("СимволыОтступа", "");
	КонецЕсли;
	Если НЕ ПараметрыЗаписи.Свойство("ИспользоватьДвойныеКавычки") Тогда
		ПараметрыЗаписи.Вставить("ИспользоватьДвойныеКавычки", Истина);
	КонецЕсли;
	Если НЕ ПараметрыЗаписи.Свойство("ЭкранированиеСимволов") Тогда
		ПараметрыЗаписи.Вставить("ЭкранированиеСимволов", ЭкранированиеСимволовJSON.Нет);
	КонецЕсли;
	Если НЕ ПараметрыЗаписи.Свойство("ЭкранироватьУгловыеСкобки") Тогда
		ПараметрыЗаписи.Вставить("ЭкранироватьУгловыеСкобки", Ложь);
	КонецЕсли;
	Если НЕ ПараметрыЗаписи.Свойство("ЭкранироватьРазделителиСтрок") Тогда
		ПараметрыЗаписи.Вставить("ЭкранироватьРазделителиСтрок", Истина);
	КонецЕсли;
	Если НЕ ПараметрыЗаписи.Свойство("ЭкранироватьАмперсанд") Тогда
		ПараметрыЗаписи.Вставить("ЭкранироватьАмперсанд", Ложь);
	КонецЕсли;
	Если НЕ ПараметрыЗаписи.Свойство("ЭкранироватьОдинарныеКавычки") Тогда
		ПараметрыЗаписи.Вставить("ЭкранироватьОдинарныеКавычки", Ложь);
	КонецЕсли;
	Если НЕ ПараметрыЗаписи.Свойство("ЭкранироватьСлеш") Тогда
		ПараметрыЗаписи.Вставить("ЭкранироватьСлеш", Ложь);
	КонецЕсли;
	ПараметрыЗаписиJSON=Новый ПараметрыЗаписиJSON(ПараметрыЗаписи.ПереносСтрок, ПараметрыЗаписи.СимволыОтступа, ПараметрыЗаписи.ИспользоватьДвойныеКавычки, ПараметрыЗаписи.ЭкранированиеСимволов, ПараметрыЗаписи.ЭкранироватьУгловыеСкобки, ПараметрыЗаписи.ЭкранироватьРазделителиСтрок, ПараметрыЗаписи.ЭкранироватьАмперсанд, ПараметрыЗаписи.ЭкранироватьОдинарныеКавычки, ПараметрыЗаписи.ЭкранироватьСлеш);

	//Параметры сериализации
	НастройкиСериализацииJSON=Новый НастройкиСериализацииJSON;
	Если НЕ ТипЗнч(ПараметрыСериализации)=Тип("Структура") Тогда
		ПараметрыСериализации=Новый Структура;
		ПараметрыСериализации.Вставить("ВариантЗаписиДаты", ВариантЗаписиДатыJSON.УниверсальнаяДата);
		ПараметрыСериализации.Вставить("СериализовыватьМассивыКакОбъекты", Ложь);
		ПараметрыСериализации.Вставить("ФорматСериализацииДаты", ФорматДатыJSON.JavaScript);
	КонецЕсли;
	Для каждого СтрокаКоллекции Из ПараметрыСериализации Цикл
		НастройкиСериализацииJSON[СтрокаКоллекции.Ключ]=СтрокаКоллекции.Значение;
	КонецЦикла;

	//Запись
	ВсеЗаписиJSON=Новый ЗаписьJSON;
	ВсеЗаписиJSON.ПроверятьСтруктуру=ПроверятьСтруктуру;
	
	Если ПараметрыФайла=Неопределено Тогда
		ВсеЗаписиJSON.УстановитьСтроку(ПараметрыЗаписиJSON);
	Иначе
		Если НЕ ПараметрыФайла.Свойство("Кодировка") Тогда
			ПараметрыФайла.Вставить("Кодировка", "UTF-8");
		КонецЕсли;
		Если НЕ ПараметрыФайла.Свойство("ДобавлятьBOM") Тогда
			ПараметрыФайла.Вставить("ДобавлятьBOM", Ложь);
		КонецЕсли;		
		ВсеЗаписиJSON.ОткрытьФайл(ПараметрыФайла.Файл, ПараметрыФайла.Кодировка, ПараметрыФайла.ДобавлятьBOM, ПараметрыЗаписиJSON);
	КонецЕсли; 

	ЗаписатьJSON(ВсеЗаписиJSON, СтруктураДанных, НастройкиСериализацииJSON);
	Возврат ВсеЗаписиJSON.Закрыть();	
КонецФункции

Функция Прочитать(Ответ, ВернутьСоответствие=Ложь, ПараметрыФайла=Неопределено) Экспорт
	#Если ВебКлиент И НЕ Сервер Тогда
		Возврат JSON_Сервер.Прочитать(Ответ, ВернутьСоответствие);
	#КонецЕсли
	ЧтениеJSON=Новый ЧтениеJSON;
	Если ПараметрыФайла=Неопределено Тогда
		ЧтениеJSON.УстановитьСтроку(?(ТипЗнч(Ответ)=Тип("HTTPОтвет"), Ответ.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8), Ответ));
	Иначе
		Если НЕ ПараметрыФайла.Свойство("Кодировка") Тогда
			ПараметрыФайла.Вставить("Кодировка", "UTF-8");
		КонецЕсли;		
		ЧтениеJSON.ОткрытьФайл(ПараметрыФайла.Файл, ПараметрыФайла.Кодировка);
	КонецЕсли;	
	Данные=ПрочитатьJSON(ЧтениеJSON, ВернутьСоответствие);
	ЧтениеJSON.Закрыть();
	Возврат Данные;
КонецФункции
